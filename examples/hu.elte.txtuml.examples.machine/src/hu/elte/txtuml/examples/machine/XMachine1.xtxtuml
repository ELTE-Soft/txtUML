package hu.elte.txtuml.examples.machine;

import XMachine1.*;

model XMachine1 {
		// classes
	
	class Machine {
		initial Init;

		state Off {
			entry {
				log("\tMachine enters state: 'off'");
			}

			exit {
				log("\tMachine exits state: 'off'");
			}
		}

		state On {
			entry {
				log("\tMachine enters state: 'on'");
			}

			exit {
				log("\tMachine exits state: 'on'");
			}

		}

		transition Initialize {
			from Init;
			to Off;
			effect {
				log("\tMachine: initializing...");
			}
		}

		transition SwitchOn {
			from Off;
			to On;
			trigger ButtonPress;
			effect {
				log("\tMachine: switching on...");
			}
		}

		transition SwitchOff {
			from On;
			to Off;
			trigger ButtonPress;
			effect {
				log("\tMachine: switching off...");
			}
		}
	}

	class User {
		initial Init;
		state Ready;
		
		transition Initialize {
			from Init;
			to Ready;
			effect {
				log("\tUser: initializing...");				
			}
		}

		transition Working {
			from Ready;
			to Ready;
			trigger DoYourWork;
			effect {
				log("\tUser: working...");
				doWork();
			}
		}
		
		void doWork() {
			log("\tUser: starting to work...");
			XMachine1.Machine myMachine = this->Usage::usedMachine.selectAny();
			send new ButtonPress() to myMachine;
			send new ButtonPress() to myMachine;
			send new ButtonPress() to myMachine;
			log("\tUser: work finished...");
		}
	}

	// associations
	
	association Usage {
		public 1 Machine usedMachine;
		public hidden * User userOfMachine;
	}

	// signals
	
	signal ButtonPress;
	signal DoYourWork;
}
