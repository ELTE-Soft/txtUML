package nuclearpower.model;

import nuclearpower.model.assocination.BatteryUIAssociation;
import nuclearpower.model.assocination.PanelBatteryComposition;

class Battery {
	
	public boolean hasCapacity () {
		return capacity > 0;
	}
	
	initial Init;
	state Passive{
		entry{
			this->(BatteryUIAssociation.ui).selectAny().gui.batteryStateChanged("Passive");
		}
	};
	state Loading{
		entry{
			this->(BatteryUIAssociation.ui).selectAny().gui.batteryStateChanged("Loading");
		}
	};
	state ProvidesEnergy{
		entry{
			this->(BatteryUIAssociation.ui).selectAny().gui.batteryStateChanged("Provides Energy");
		}
	};
	
	transition Initialize {
		from Init;
		to Passive;
		effect{
			this->(BatteryUIAssociation.ui).selectAny().gui.batteryCapacityChanged(capacity);
		}
	}
	
	transition StartCharging {
		from Passive;
		to Loading;
		trigger ChargingBattery;
		effect {
			capacity++;
			this->(BatteryUIAssociation.ui).selectAny().gui.batteryCapacityChanged(capacity);
		}
		guard (capacity < maxCapacity);
	}
	
	transition ChargingFromSunEnergy {
		from Loading;
		to Loading;
		trigger ChargingBattery;
		effect {
			capacity++;
			this->(BatteryUIAssociation.ui).selectAny().gui.batteryCapacityChanged(capacity);
		}
		guard (capacity < maxCapacity);
	}
	
	transition FinishCharging {
		from Loading;
		to Passive;
		trigger ChargingBattery;
		guard (capacity == maxCapacity);
	}
	
	transition WatherBecomeCloudy {
		from Loading;
		to Passive;
		trigger StopChargingBattery;
	}
	
	
	
	transition StartUsing {
		from Passive;
		to ProvidesEnergy;
		trigger UsingBattery;
		guard (capacity > 0);
		effect {
			capacity--;
			this->(BatteryUIAssociation.ui).selectAny().gui.batteryCapacityChanged(capacity);
		}
	}
	
	transition ProvidingEnergy {
		from ProvidesEnergy;
		to ProvidesEnergy;
		trigger UsingBattery;
		guard (capacity > 1);
		effect {
			capacity--;
			this->(BatteryUIAssociation.ui).selectAny().gui.batteryCapacityChanged(capacity);		
		}
	}
	
	transition RunningOutFromEnergy {
		from ProvidesEnergy;
		to Passive;
		trigger UsingBattery;
		guard (capacity == 1);
		effect {
			capacity--;
			SolarPanel panel = this->(PanelBatteryComposition.panel).selectAny();
			send new BatteryRunOutFromEnergy() to panel;
			this->(BatteryUIAssociation.ui).selectAny().gui.batteryCapacityChanged(capacity);
		}
	}
	
	public Battery (int maxCapacity) {
		this.maxCapacity = maxCapacity;
		capacity = 0;
	}
	
	private int capacity;
	private int maxCapacity;
}
