package hu.elte.txtuml.examples.xproducer_consumer;

import hu.elte.txtuml.api.^model.ModelExecutor;

signal DoWork;
signal Acknowledgement;

class Producer {
	int toProduce;
	Producer(int num) {
		toProduce = num;
	}
	
	Item produce() {
		this.toProduce--;
		send new DoWork() to Producer.this;
		return new Item();
	}
	
	initial Init;
	state Active;
	state Passive {
		entry {
			Storage storage = Producer.this->Production::storage.selectAny();
			unlink(Production.producer, Producer.this, Production.storage, storage);
			delete Producer.this;
		}
	}
	
	transition Initialize {
		from Init;
		to Active;
		effect {
			send new DoWork() to Producer.this;
		}
	}
	
	transition DoOffer {
		from Active;
		to Active;
		trigger DoWork;
		guard (Producer.this.toProduce > 0);
		effect {
			Storage storage = Producer.this->Production::storage.selectAny();
			link(Offer.producer, Producer.this, Offer.storage, storage);
			send new OfferNotification() to storage;
		}
	}
	
	transition Stop {
		from Active;
		to Passive;
		trigger DoWork;
		guard (Producer.this.toProduce <= 0);
	}
}

class Consumer {
	int toConsume;
	Consumer(int toConsume) {
		this.toConsume = toConsume;
	}
	
	void consume(Item item) {
		toConsume--;
		send new DoWork() to Consumer.this;
	}
	
	initial Init;
	state Active;
	state Passive {
		entry {
			Storage storage = Consumer.this->Consumption::storage.selectAny();
			unlink(Consumption.consumer, Consumer.this, Consumption.storage, storage);
			delete Consumer.this;
		}
	}
	
	transition Initialize {
		from Init;
		to Active;
		effect {
			send new DoWork() to Consumer.this;
		}
	}
	
	transition DoRequest {
		from Active;
		to Active;
		trigger DoWork;
		guard (Consumer.this.toConsume > 0)
		effect {
			Storage storage = Consumer.this->Consumption::storage.selectAny();
			link(Request.storage, storage, Request.consumer, Consumer.this);
			send new RequestNotification() to storage;
		}
	}
	
	transition Stop {
		from Active;
		to Passive;
		trigger DoWork;
		guard (Consumer.this.toConsume <= 0);
	}
}

signal OfferNotification;
signal RequestNotification;

class Storage {
	int capacity;
	
	Storage(int capacity) {
		this.capacity = capacity;
	}
	
	initial Init;
	state Working;
	
	transition Initialize {
		from Init;
		to Working;
	}
	
	transition CanAccept {
		from Working;
		to Working;
		trigger OfferNotification;
		guard(Storage.this->InStore::item.count() < capacity);
		effect {
			Producer producer = Storage.this->Offer::producer.selectAny();
			unlink(Offer.producer, producer, Offer.storage, Storage.this);
			Item item = producer.produce();
			link(InStore.storage, Storage.this, InStore.item, item);
		}
	}
	
	transition CannotAccept {
		from Working;
		to Working;
		trigger OfferNotification;
		guard(Storage.this->InStore::item.count() >= capacity);
		effect {
			// postpone the offer
			send new OfferNotification() to Storage.this;
		}
	}

	transition CanServe {
		from Working;
		to Working;
		trigger RequestNotification;
		guard(Storage.this->InStore::item.count() > 0);
		effect {
			Consumer consumer = Storage.this->Request::consumer.selectAny();
			unlink(Request.storage, Storage.this, Request.consumer, consumer);
			Item item = Storage.this->InStore::item.selectAny();
			unlink(InStore.storage, Storage.this, InStore.item, item);
			consumer.consume(item);
		}
	}
	
	transition CannotServe {
		from Working;
		to Working;
		trigger RequestNotification;
		guard(Storage.this->InStore::item.count() <= 0);
		effect {
			// postpone the request
			send new RequestNotification() to Storage.this;
		}
	}
	
}

class Item;

association Production {
	* Producer producer;
	1 Storage storage;
}

association Offer {
	* Producer producer;
	0..1 Storage storage;
}

association Consumption {
	1 Storage storage;
	* Consumer consumer;
}

association Request {
	0..1 Storage storage;
	* Consumer consumer;
}

association InStore {
	* Item item;
	0..1 Storage storage;
}

execution XProducerConsumerTester {
	ModelExecutor.Settings.setExecutorLog(true);

	Storage storage = create(Storage,2);
	Producer p1 = create(Producer,3);
	Producer p2 = create(Producer,3);
	Consumer c1 = create(Consumer,2);
	Consumer c2 = create(Consumer,2);
	Consumer c3 = create(Consumer,2);

	link(Production.producer, p1, Production.storage, storage);
	link(Production.producer, p2, Production.storage, storage);
	link(Consumption.consumer, c1, Consumption.storage, storage);
	link(Consumption.consumer, c2, Consumption.storage, storage);
	link(Consumption.consumer, c3, Consumption.storage, storage);

	start(storage);
	start(p1);
	start(p2);
	start(c1);
	start(c2);
	start(c3);

	ModelExecutor.shutdown();
}
