package monitoring.x.model;

signal Error;
signal OK;

class Alert {
	int max;
	int level;
	
	public Alert(int max) {
		this.max = max;
		this.level = 0;
	}

	initial Init;
	state Normal;
	state Critical;
	
	transition TInitialize {
		from Init;
		to Normal;
	}
	
	transition TIncreaseLevel {
		from Normal;
		to Normal;
		trigger Error;
		guard (level < max)
		effect {
			level++;
		}
	}
	
	transition TStartAlert {
		from Normal;
		to Critical;
		trigger Error;
		guard (level >= max)
		effect {
			log "!!! Critical number of errors detected !!!";
		}
	}
	
	transition TStopAlert {
		from Critical;
		to Normal;
		trigger OK;
		effect {
			log "+++ Back to normal operation +++";
			level = 0;
		}
	}
}
