package train.x;

import hu.elte.txtuml.api.model.^execution.LogLevel;
import train.x.model.Backward;
import train.x.model.Engine;
import train.x.model.Forward;
import train.x.model.GE;
import train.x.model.GL;
import train.x.model.Gearbox;
import train.x.model.LE;
import train.x.model.Lamp;
import train.x.model.SwitchLight;

execution XDemo {

	configure {
		logLevel = LogLevel.TRACE;
	}

	Gearbox g;

	Engine e;

	Lamp l;

	initialization {
		g = create Gearbox();
		e = create Engine();
		l = create Lamp();
		link g, e via GE;
		link g, l via GL;
		link l, e via LE;
		start g;
		start e;
		start l;
	}

	during {
		int time = 50;
		for (int i = 0; i < 3; i++) {
			Thread.sleep(time);
			log "";
			send new SwitchLight() to l;
		}

		Thread.sleep(2 * time);

		for (int i = 0; i < 3; i++) {
			Thread.sleep(3 * time);
			log "";
			send new Forward() to g;

			Thread.sleep(time);
			log "";
			send new Backward() to g;
		}
	}

}

