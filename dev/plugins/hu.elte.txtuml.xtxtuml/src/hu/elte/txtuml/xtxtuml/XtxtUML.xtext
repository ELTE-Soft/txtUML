grammar hu.elte.txtuml.xtxtuml.XtxtUML with org.eclipse.xtext.xbase.Xbase
generate xtxtUML "http://www.elte.hu/txtuml/xtxtuml/XtxtUML"

import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/xtext/xbase/Xtype" as xtype

// Start rule

TUFile :
	TUModelDeclaration |
	(('package' name=QualifiedName ';'?)?
	importSection=XImportSection?
	elements+=TUModelElement*)
;

TUModelDeclaration :
	{TUModelDeclaration}
	model='model-package' name=QualifiedName ('as' modelName=STRING)? semiColon=';'?
		// feature 'semiColon' is required because of the buggy formatter2 API
;

// Model level

TUModelElement
	: TUExecution
	| TUSignal
	| TUClass
	| TUEnumeration
	| TUAssociation
	| TUInterface
	| TUConnector
;

TUExecution :
	'execution' name=ValidID ('{'
		elements+=TUExecutionElement* 
	'}' ';'? | ';')?
;

TUSignal :
	'signal' name=ValidID ('extends' superSignal=[TUSignal|QualifiedName])? ('{'
		attributes+=TUSignalAttribute*
	'}' ';'? | ';')
;

TUClass : 
	'class' name=ValidID ('extends' superClass=[TUClass|QualifiedName])? ('{'
		members+=TUClassMember*
	'}' ';'? | ';')
;

TUEnumeration :
	'enum' name=ValidID ('{'
		(literals+=TUEnumerationLiteral (',' literals+=TUEnumerationLiteral)*)? 
	'}' ';'? | ';')
;

TUEnumerationLiteral :
	name=ValidID
;

TUAssociation returns TUAssociation:
	({TUAssociation} 'association' | {TUComposition} 'composition') name=ValidID '{'
		ends+=TUAssociationEnd*
	'}' ';'?
;

TUInterface :
	'interface' name=ValidID '{'
		receptions+=TUReception*
	'}' ';'?
;

TUReception :
	'reception' signal=[TUSignal|QualifiedName] ';'
;

TUConnector :
	('connector' | delegation?='delegation') name=ValidID '{'
		ends+=TUConnectorEnd*
	'}' ';'?
;

TUConnectorEnd :
	role=[TUAssociationEnd|QualifiedName] '->' port=[TUPort|QualifiedName] name=ValidID ';'
;

// Inner element level

// Execution related

TUExecutionElement :
	type=TUExecutionElementType body=XBlockExpression ';'?
;

enum TUExecutionElementType 
	: CONFIGURE = 'configure' | INITIALIZATION = 'initialization' | BEFORE = 'before'
	| DURING = 'during' | AFTER = 'after'
;

// Signal- and Class-related

TUSignalAttribute :
	visibility=TUVisibility? type=JvmTypeReference name=ValidID ';'
;

TUClassMember
	: TUConstructor
	| TUAttributeOrOperationDeclarationPrefix (
		{TUAttribute.prefix=current} name=ValidID ('=' initExpression=XExpression)? ';' |
		{TUOperation.prefix=current} name=ValidID '(' (parameters+=FullJvmFormalParameter (',' parameters+=FullJvmFormalParameter)*)? ')'
			body=XBlockExpression ';'?
	  )
	| TUState
	| TUTransition
	| TUPort
;

/*
 * The inferred <code>EClass</code> is used in TUClassPropertyAccessExpression.
 */
TUClassProperty :
	TUPort | TUAssociationEnd
;

TUConstructor :
	modifiers=TUModifiers name=ValidID '(' (parameters+=FullJvmFormalParameter (',' parameters+=FullJvmFormalParameter)*)? ')'
		body=XBlockExpression ';'?
;

TUAttributeOrOperationDeclarationPrefix :
	modifiers=TUModifiers type=JvmTypeReference
;

TUState :
	type=TUStateType name=ValidID ('{'
		members+=TUStateMember* 
	'}' ';'? | ';')
;

enum TUStateType :
	PLAIN = 'state' | INITIAL = 'initial' | CHOICE = 'choice' | COMPOSITE = 'composite'
;

TUStateMember :
	TUState | TUEntryOrExitActivity | TUTransition
;

/*
 * The inferred <code>EClass</code> is used in unit tests.
 */
TUClassOrStateMember :
	TUClassMember | TUStateMember
;

TUEntryOrExitActivity :
	(entry?='entry' | exit='exit') // feature 'exit' is for error markers
		body=XBlockExpression ';'?
;

TUTransition :
	'transition' name=ValidID '{'
		members+=TUTransitionMember*
	'}' ';'?
;

TUTransitionMember
	: TUTransitionTrigger
	| TUTransitionVertex
	| TUTransitionEffect
	| TUTransitionGuard
	| TUTransitionPort
;

TUTransitionTrigger :
	triggerKeyword='trigger' trigger=[TUSignal|QualifiedName] ';'
		// feature 'triggerKeyword' is for error markers
;

TUTransitionVertex :
	(from?='from' | to='to') vertex=[TUState|QualifiedName] ';'
		// feature 'to' is for error markers
;

TUTransitionEffect :
	effect='effect' body=XBlockExpression ';'?
		// feature 'effect' is for error markers
;

TUTransitionGuard :
	{TUTransitionGuard} // required because of the buggy formatter2 API
	guard='guard' '(' (else?='else' | expression=XExpression) ')' ';'?
		// feature 'guard' is for error markers
;

TUTransitionPort :
	portKeyword='port' port=[TUPort|QualifiedName] ';'
		// feature 'portKeyword' is for error markers
;

TUPort :
	behavior?='behavior'? 'port' name=ValidID '{'
		members+=TUPortMember*
	'}' ';'?
;

TUPortMember :
	(required?='required' | provided='provided') interface=[TUInterface|QualifiedName] ';'
		// feature 'provided' is for error markers
;

// Association-related

TUAssociationEnd :
	visibility=TUVisibility? notNavigable?='hidden'?
		multiplicity=TUMultiplicity? container?='container'?
		endClass=[TUClass|QualifiedName] name=ValidID ';'
;

TUMultiplicity :
	lower=INT (upperSet?='..' (upper=INT | upperInf?='*'))? | any?='*'
;

// Commons

TUModifiers:
	{TUModifiers} (static?='static'? & visibility=TUVisibility? & externality=TUExternality?)
;

enum TUVisibility :
	PACKAGE = 'package' | PRIVATE = 'private' | PROTECTED = 'protected' | PUBLIC = 'public'
;

enum TUExternality :
	NON_EXTERNAL = 'non-external' | EXTERNAL = 'external' | EXTERNAL_BODY = 'external-body' 
;

// Actions

TUSendSignalExpression returns xbase::XExpression :
	{TUSendSignalExpression}
	'send' signal=XExpression 'to' target=XExpression
;

TUDeleteObjectExpression returns xbase::XExpression :
	{TUDeleteObjectExpression}
	'delete' object=XExpression
;

TUStartObjectExpression returns xbase::XExpression :
	{TUStartObjectExpression}
	'start' object=XExpression
;

TULogExpression returns xbase::XExpression:
	{TULogExpression}
	('log' | error?='log-error') message=XExpression
;

TUBindExpression returns xbase::XExpression :
	{TUBindExpression}
	type=TUBindType leftParticipant=XMemberFeatureCall (leftAs='as' leftEnd=[TUConnectiveEnd|QualifiedName]?)? ','
		rightParticipant=XMemberFeatureCall (rightAs='as' rightEnd=[TUConnectiveEnd|QualifiedName]?)?
		via='via' connective=[TUConnective|QualifiedName]?
			// features 'leftAs', 'rightAs' and 'via' are for error markers
;

enum TUBindType :
	LINK = 'link' | UNLINK = 'unlink' | CONNECT = 'connect'
;

TUConnectiveEnd :
	// it is inevitable to include 'TUPort' here
	// otherwise 'TUAssociationEnd' would derive both from 'TUClassProperty' and 'TUConnectiveEnd'
	// which would result in a duplicate 'name' feature in it, rendering the grammar incorrect
	// by not flattening 'TUClassProperty', at least the implementation of '->' could remain intact
	TUClassProperty | TUConnectorEnd
;

TUConnective :
	TUAssociation | TUConnector
;

TUSignalAccessExpression returns xbase::XExpression :
	{TUSignalAccessExpression}
	trigger='trigger'
		// feature 'trigger' is for error markers
;

TUStatementSemicolonNotNeeded returns xbase::XExpression
	: XIfExpression
	| XForLoopExpression
	| XBasicForLoopExpression
	| XWhileExpression
	| XSwitchExpression
	| XBlockExpression
	| XVariableDeclaration // semicolon included in rule
;

TUStatementSemicolonNeeded returns xbase::XExpression
	: XReturnExpression
	| XDoWhileExpression
	| XExpression
	| TUSendSignalExpression
	| TUStartObjectExpression
	| TULogExpression
	| TUDeleteObjectExpression
	| TUBindExpression
;

// Overridden Xbase rules

/*
 * Changes:
 * <ul>
 *     <li>
 *         Removed:
 *         <ul>
 *             <li>XCollectionLiteral</li>
 *             <li>XClosure</li>
 *             <li>XTypeLiteral</li>
 *         </ul>
 *     </li>
 * </ul>
 */
XLiteral returns xbase::XExpression
	: XBooleanLiteral
	| XNumberLiteral
	| XNullLiteral
	| XStringLiteral
;

/*
 * Changes:
 * <ul>
 *     <li>
 *         Removed: 
 *         <ul>
 *             <li>XBlockExpression</li>
 *             <li>XSwitchExpression</li>
 *             <li>XSynchronizedExpression</li>
 *             <li>XIfExpression</li>
 *             <li>XForLoopExpression</li>
 *             <li>XBasicForLoopExpression</li>
 *             <li>XWhileExpression</li>
 *             <li>XDoWhileExpression</li>
 *             <li>XThrowExpression</li>
 *             <li>XReturnExpression</li>
 *             <li>XTryCatchFinallyExpression</li>
 *         </ul>
 *     </li>
 *     <li>
 *         Added:
 *         <ul>
 *             <li>TUSignalAccessExpression</li>
 *         </ul>
 *     </li>
 * </ul>
 */
XPrimaryExpression returns xbase::XExpression
	: XConstructorCall
	| XFeatureCall
	| XLiteral
	| XParenthesizedExpression
	| TUSignalAccessExpression
;

/*
 * Changes:
 * <ul>
 *     <li>at most one "simple" type argument</li>
 *     <li>parentheses are mandatory</li>
 *     <li>no XShortClosure in arguments</li>
 *     <li>no XClosure after the parameter list</li>
 * </ul>
 */
XConstructorCall returns xbase::XConstructorCall :
	{TUCreateObjectExpression}
	(new?='new' | 'create') constructor=[types::JvmConstructor|QualifiedName]
		=>explicitConstructorCall?='(' (arguments+=XExpression (',' arguments+=XExpression)*)? ')'
	('as' objectName=STRING)?
;

/*
 * Changes:
 * <ul>
 *     <li>restructured contained expressions</li>
 * </ul>
 */
XBlockExpression returns xbase::XExpression : 
	{xbase::XBlockExpression}
	'{' 
		(expressions+=TUStatementSemicolonNotNeeded | =>(expressions+=TUStatementSemicolonNeeded) ';' )*
			// don't change the syntactic predicate here, it would break the grammar
	'}'
;

/* 
 * Changes:
 * <ul>
 *     <li>body is an XBlockExpression instead of an XExpression</li>
 * </ul>
 */
XWhileExpression returns xbase::XExpression :
	{xbase::XWhileExpression}
	'while' '(' predicate=XExpression ')'
		body=XBlockExpression
;

/* 
 * Changes:
 * <ul>
 *     <li>body is an XBlockExpression instead of an XExpression</li>
 * </ul>
 */
XDoWhileExpression returns xbase::XExpression :
	{xbase::XDoWhileExpression}
	'do'
		body=XBlockExpression
	'while' '(' predicate=XExpression ')'
;

/*
 * Changes:
 * <ul>
 *     <li>no closures after parentheses</li>
 *     <li>no type parameters</li>
 * </ul>
 */
XFeatureCall returns xbase::XExpression :
	{xbase::XFeatureCall}
	feature=[types::JvmIdentifiableElement|IdOrSuper] (
		=>explicitOperationCall?='(' (
			  featureCallArguments+=XShortClosure
			| featureCallArguments+=XExpression (',' featureCallArguments+=XExpression)*
		)? ')'
	)?
;

/*
 * Changes:
 * <ul>
 *     <li>XBlockExpression is mandatory, except for an "else if" construct</li>
 * </ul>
 */
XIfExpression returns xbase::XExpression :
	{xbase::XIfExpression}
	'if' '(' if=XExpression ')'
		then=XBlockExpression
	(=>'else' else=(XIfExpression | XBlockExpression))?
;

/*
 * Changes:
 * <ul>
 *     <li>JvmFormalParameter -> FullJvmFormalParameter (type is mandatory)</li>
 *     <li>XBlockExpression is mandatory</li>
 * </ul>
 */
XForLoopExpression returns xbase::XExpression :
	=>({xbase::XForLoopExpression}
	'for' '(' declaredParam=FullJvmFormalParameter ':') forExpression=XExpression ')'
		eachExpression=XBlockExpression
;

/*
 * Changes:
 * <ul>
 *     <li>no variable declaration allowed</li>
 *     <li>parentheses are mandatory</li>
 *     <li>default has to be a block</li>
 * </ul>
 */
XSwitchExpression returns xbase::XExpression :
	{xbase::XSwitchExpression}
	'switch' '(' switch=XExpression ')' '{'
		cases+=XCasePart*
		('default' ':' default=XBlockExpression)?
	'}'
;

/*
 * Changes:
 * <ul>
 *     <li>removed type guards (thus 'case' is mandatory)</li>
 *     <li>then attribute can only be a block</li>
 * </ul>
 */
XCasePart returns xbase::XCasePart :
	{xbase::XCasePart}
	'case' case=XExpression (':' then=XBlockExpression | fallThrough?=',')
;

/*
 * Changes:
 * <ul>
 *     <li>only one init and update expression is allowed</li>
 *     <li>XBlockExpression is mandatory</li>
 * </ul>
 */
XBasicForLoopExpression returns xbase::XBasicForLoopExpression : 
	{xbase::XBasicForLoopExpression}
	'for' '(' (initExpressions+=XExpression ';' | initExpressions+=XVariableDeclaration | ';') 
		expression=XExpression? ';' updateExpressions+=XExpression? ')'
			eachExpression=XBlockExpression
;

/*
 * Changes:
 * <ul>
 *     <li>removed 'var' and 'val' keywords</li>
 *     <li>type is mandatory</li>
 * </ul>
 */
XVariableDeclaration returns xbase::XExpression :
	=>({xbase::XVariableDeclaration} type=JvmTypeReference name=ValidID)
		('=' right=XExpression)? writeable?=';'
;

/*
 * Changes:
 * <ul>
 *     <li>only delegates to XAdditiveExpression</li>
 * </ul>
 */
XOtherOperatorExpression returns xbase::XExpression :
	XAdditiveExpression
;

/*
 * Changes:
 * <ul>
 *     <li>no nullsafe feature call keyword</li>
 *     <li>no type arguments for features</li>
 *     <li>no closure after parenthesis</li>
 *     <li>added class property access expression</li>
 * </ul>
 */
XMemberFeatureCall returns xbase::XExpression :
	XPrimaryExpression
	(
		=>(
			{xbase::XAssignment.assignable=current} ('.' | explicitStatic?='::')
			feature=[types::JvmIdentifiableElement|FeatureCallID] OpSingleAssign
		)
		value=XAssignment
	| 
		=>(
			{xbase::XMemberFeatureCall.memberCallTarget=current} ('.' | explicitStatic?='::')
		) 
		feature=[types::JvmIdentifiableElement|IdOrSuper] (
			=>explicitOperationCall?='(' (
				  memberCallArguments+=XShortClosure
				| memberCallArguments+=XExpression (',' memberCallArguments+=XExpression)*
			)? ')'
		)?
	| 
		=>({TUClassPropertyAccessExpression.left=current} arrow='->') '('
			// feature 'arrow' is for error markers
			right=[TUClassProperty|QualifiedName]?
		')'
	)*
;

// New Xtype rules

JvmSimpleTypeReference returns types::JvmParameterizedTypeReference :
	{types::JvmParameterizedTypeReference} type=[types::JvmType|QualifiedName]
;

// Overridden Xtype rules

/*
 * Changes:
 * <ul>
 *     <li>
 *         Removed:
 *         <ul>
 *             <li>JvmGenericArrayTypeReference</li>
 * 	           <li>XFunctionTypeRef</li>
 *         </ul>
 *     </li>
 *     <li>
 *         Added:
 *         <ul>
 *             <li>JvmSimpleTypeReference</li>
 *         </ul>
 *     </li>
 * </ul> 
 */
JvmTypeReference returns types::JvmTypeReference :
	JvmParameterizedTypeReference | JvmSimpleTypeReference
;

/*
 * Changes:
 * <ul>
 *     <li>exactly one "simple" type argument</li>
 * </ul>
 */
JvmParameterizedTypeReference returns types::JvmParameterizedTypeReference :
	=>(type=[types::JvmType|QualifiedName] '<') arguments+=JvmSimpleTypeReference '>'
;

/*
 * Changes:
 * <ul>
 *     <li>removed extension imports</li>
 * </ul>
 */
XImportDeclaration returns xtype::XImportDeclaration : 
	'import' (
		(static?='static' importedType=[types::JvmDeclaredType|QualifiedNameInStaticImport] (wildcard?='*' | memberName=ValidID)) 
		| importedType=[types::JvmDeclaredType|QualifiedName] 
		| importedNamespace=QualifiedNameWithWildcard) ';'?
;
